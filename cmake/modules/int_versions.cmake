# top-level CMakeLists.txt has defined:
# * PROJECT_VERSION_{MAJOR|MINOR|PATCH} through `project(... VERSION)`
# * LIBINT_BUILDID
# * LIBINT_SOVERSION

# note that 3rd version integer is PATCH in CMake and MICRO in Libint


# <<<  Build Version  >>>

set(LIBINT_MAJOR_VERSION ${PROJECT_VERSION_MAJOR})
set(LIBINT_MINOR_VERSION ${PROJECT_VERSION_MINOR})
set(LIBINT_MICRO_VERSION ${PROJECT_VERSION_PATCH})

set(LIBINT_VERSION ${LIBINT_MAJOR_VERSION}.${LIBINT_MINOR_VERSION}.${LIBINT_MICRO_VERSION})


# <<<  Dev Version  >>>

if (LIBINT_BUILDID)
  set(LIBINT_EXT_VERSION ${LIBINT_VERSION}-${LIBINT_BUILDID})
else()
  set(LIBINT_EXT_VERSION ${LIBINT_VERSION})
endif()

message(STATUS "Version: Full ${LIBINT_EXT_VERSION} Numeric ${LIBINT_VERSION}")


# <<<  ABI Version  >>>

string(REPLACE ":" ";" LIBINT_SOVERSION_LIST ${LIBINT_SOVERSION})

list(GET LIBINT_SOVERSION_LIST 0 LIBINT_CURRENT_SOVERSION)
list(GET LIBINT_SOVERSION_LIST 1 LIBINT_REVISION_SOVERSION)
list(GET LIBINT_SOVERSION_LIST 2 LIBINT_AGE_SOVERSION)

math(EXPR LIBINT_MAJOR_SOVERSION "${LIBINT_CURRENT_SOVERSION} - ${LIBINT_AGE_SOVERSION}")
message(STATUS "SO Version: Full ${LIBINT_SOVERSION} Major ${LIBINT_MAJOR_SOVERSION}")
